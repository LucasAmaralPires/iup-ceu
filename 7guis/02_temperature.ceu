native _{
#if 0
int txt_celcius_cb(Ihandle *self)
{
  Ihandle* fahrenheit = IupGetDialogChild(self, "FAHRENHEIT");
  double value = IupGetDouble(self, "VALUE");;

  value = value * (9./5.) + 32;

  IupSetStrf(fahrenheit, "VALUE", "%.2lf", value);

  return IUP_DEFAULT;
}

int txt_fahrenheit_cb(Ihandle *self)
{
  Ihandle* celcius = IupGetDialogChild(self, "CELCIUS");
  double value = IupGetDouble(self, "VALUE");;

  value = (value - 32) * (5./9.);

  IupSetStrf(celcius, "VALUE", "%.2lf", value);

  return IUP_DEFAULT;
}

int main(int argc, char **argv)
{

  /* Registers callbacks */
  IupSetCallback(tempC, "VALUECHANGED_CB", (Icallback)txt_celcius_cb);
  IupSetCallback(tempF, "VALUECHANGED_CB", (Icallback)txt_fahrenheit_cb);

}
#endif
}

^"event.ceu"
^"int.ceu"
^"long.ceu"
^"iup.ceu"

output iup Iup.Open

spawn {
    var labelC: _(Ihandle*)
    var labelF: _(Ihandle*)
    {
        output iup Iup.Label [/labelC, _("Celsius =")]
        output iup Iup.Label [/labelF, _("Fahrenheit")]
    }

    var tempC: _(Ihandle*)
    {
        output iup Iup.Text   /tempC
        output iup Iup.Set.Str [tempC, _("SIZE"), _("50")]
        output iup Iup.Set.Str [tempC, _("MASK"), _IUP_MASK_FLOAT]
        output iup Iup.Set.Str [tempC, _("VALUE"), _("")]
    }

    var tempF: _(Ihandle*)
    {
        output iup Iup.Text   /tempF
        output iup Iup.Set.Str [tempF, _("SIZE"), _("50")]
        output iup Iup.Set.Str [tempF, _("MASK"), _IUP_MASK_FLOAT]
        output iup Iup.Set.Str [tempF, _("VALUE"), _("")]
    }

    var hbox: _(Ihandle*)
    {
        output iup Iup.HBox   /hbox
        output iup Iup.Append [hbox, tempC]
        output iup Iup.Append [hbox, labelC]
        output iup Iup.Append [hbox, tempF]
        output iup Iup.Append [hbox, labelF]
        output iup Iup.Set.Str [hbox, _("MARGIN"), _("10x10")]
        output iup Iup.Set.Str [hbox, _("GAP"), _("10")]
        output iup Iup.Set.Str [hbox, _("ALIGNMENT"), _("ACENTER")]
    }

    var dlg: _(Ihandle*)
    {
        output iup Iup.Dialog [/dlg, hbox]
        output iup Iup.Set.Str [hbox, _("TITLE"), _("Temperature")]
    }

    output iup Iup.ShowXY [dlg, _IUP_CENTER, _IUP_CENTER]

    par {
        every isEventText [/evt,tempC] {
            var v: _float
            output iup Iup.Get.Float [tempC, _("VALUE"), /v]
            output iup Iup.Set.StrF  [tempF, _("VALUE"), _("%.2lf"), _($v * (9./5.) + 32)]
        }
    } with {
        every isEventText [/evt,tempF] {
            var v: _float
            output iup Iup.Get.Float [tempF, _("VALUE"), /v]
            output iup Iup.Set.StrF  [tempC, _("VALUE"), _("%.2lf"), _(($v - 32) * (5./9.))]
        }
    }
}

output iup Iup.Loop
output iup Iup.Close

