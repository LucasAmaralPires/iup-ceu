^["@/iup/iup.ceu"]

data :FName = [nm, snm, id]

;;;
TODO - 
    Refazer a função trim
    Colocar variaveis globais dentro do spawn
    Erro na criação databese com 80
;;;


val MAX_NAMES = 80
val db_names_list :FName = #[]
var db_names_count = 0
var db_names_id = 0

;;;;;;;;;;;;;;;;;;;;;;;;; OPERATIONS ;;;;;;;;;;;;;;;;;;;;;;;;;

func strLower(word) {
    var temp_word = ""
    loop c in word {
        if (to.number(c) >= 65) and (to.number(c) <= 90) {
            set temp_word[+] = to.char(to.number(c) + 32)
        } else {
            set temp_word[+] = c
        }
    }
    temp_word
}

func filterName(surname, filter) {
    val temp_surname = strLower(surname)
    val temp_filter = strLower(filter)
    var i = 0
    if(#temp_filter > #temp_surname){
        return(false)
    }
    loop c in temp_filter {
        if (c /= temp_surname[i]) {
            return(false)
        }
        set i = i + 1
    }
    true
}

func trim (word) {
    var trim_word = ""
    var temp_word = ""
    var start_word = false
    var end_word = false
    loop c in word {
        ifs {
            (c /= ' ')  and (start_word == false) {
                set trim_word[+] = c
                set start_word = true
            }
            (c == ' ') and (start_word == true) and (end_word == true){
                set temp_word[+] = c
            }
            (c == ' ') and (start_word == true) {
                set end_word = true
                set temp_word[+] = c
            }
            (c /= ' ')  and (end_word == true) {
                set trim_word = trim_word ++ temp_word
                set trim_word[+] = c
                set temp_word = ""
                set end_word = false
            }
            (start_word == true) and (end_word == false) {
                set trim_word[+] = c
            }
        } 
    }
    trim_word
}

spawn {
    
    val lbl_filter = iup.label("Filter Prefix: ")

    val txt_filter = iup.text()
        iup.set(txt_filter, :EXPAND, "HORIZONTAL")
        iup.set(txt_filter, :SIZE, "60x")

    val lst_names = iup.list()
        iup.set(lst_names, :EXPAND, "YES")
        iup.set(lst_names, :SIZE, "60x")
        iup.set(lst_names, :VISIBLELINES, "6")

    val lbl_name = iup.label("Name:")
        iup.set(lbl_name, :SIZE, "35")

    val txt_name = iup.text()
        iup.set(txt_name, :SIZE, "60")
        iup.set(txt_name, :NC, "80")
        iup.set(txt_name, :EXPAND, "HORIZONTAL")

    val lbl_surname = iup.label("Surname:")
        iup.set(lbl_surname, :SIZE, "35")

    val txt_surname = iup.text()
        iup.set(txt_surname, :EXPAND, "HORIZONTAL")
        iup.set(txt_surname, :SIZE, "60")
        iup.set(txt_surname, :NC, "80")

    val btn_create = iup.button("Create")
        iup.set(btn_create, :SIZE, "30")

    val btn_update = iup.button("Update")
        iup.set(btn_update, :SIZE, "30")
        iup.set(btn_update, :ACTIVE, "NO")

    val btn_delete = iup.button("Delete")
        iup.set(btn_delete, :SIZE, "30")
        iup.set(btn_delete, :ACTIVE, "NO")

    val hbx_filter = iup.hbox()
        iup.append(hbx_filter, lbl_filter)
        iup.append(hbx_filter, txt_filter)
        iup.set(hbx_filter, :ALIGNMENT, "ACENTER")

    val hbx_top = iup.hbox()
        iup.append(hbx_top, hbx_filter)
        iup.append(hbx_top, iup.fill())
        iup.set(hbx_top, :HOMOGENEOUS, "YES")

    val hbx_name = iup.hbox()
        iup.append(hbx_name, lbl_name)
        iup.append(hbx_name, txt_name)
        iup.set(hbx_name, :ALIGNMENT, "ACENTER")

    val hbx_surname = iup.hbox()
        iup.append(hbx_surname, lbl_surname)
        iup.append(hbx_surname, txt_surname)
        iup.set(hbx_surname, :ALIGNMENT, "ACENTER")

    val hbx_buttons = iup.hbox()
        iup.append(hbx_buttons, btn_create)
        iup.append(hbx_buttons, btn_update)
        iup.append(hbx_buttons, btn_delete)

    val vbx_input = iup.vbox()
        iup.append(vbx_input,hbx_name)
        iup.append(vbx_input,hbx_surname)

    val hbox = iup.hbox()
        iup.append(hbox, lst_names)
        iup.append(hbox, vbx_input)
        iup.set(hbox, :HOMOGENEOUS, "YES")

    val box = iup.vbox()
        iup.append(box, hbx_top)
        iup.append(box, hbox)
        iup.append(box, hbx_buttons)
        iup.set(box, :NMARGIN, "10x10")

    val dlg = iup.dialog(box)
        iup.set(dlg, :TITLE, "CRUD")
        iup.set(dlg, :GAP, "10")

    iup.show-xy(dlg, `:number IUP_CENTER`, `:number IUP_CENTER`)

    loop {
        val loadupdate = par-or {
            await :Iup.Action | it.h==btn_create {
                val name_value = iup.get.pointer(txt_name, :VALUE) -> to-string -> trim
                val surname_value = iup.get.pointer(txt_surname, :VALUE) -> to-string -> trim

                ;;;
                if db_names_count == 80 {
                    error
                }
                ;;;

                set db_names_list[db_names_count] = [name_value,surname_value,db_names_id]

                set db_names_count = db_names_count + 1
                set db_names_id = db_names_id + 1

                iup.set(txt_name, :VALUE, nil)
                iup.set(txt_surname, :VALUE, nil)
            }
            [true,true]
        } with {
            await :Iup.Action | it.h==btn_update {
                val id_val = iup.get.number(lst_names, :VALUE)
                val id = iup.get.id(lst_names, :ID, id_val)
                val name_value = iup.get.pointer(txt_name, :VALUE) -> to-string -> trim
                val surname_value = iup.get.pointer(txt_surname, :VALUE) -> to-string -> trim

                val found = loop i in {0 => db_names_count{ {
                    if (db_names_list[i][2] == id){
                        break(i)
                    }
                }

                set db_names_list[found][0] = name_value
                set db_names_list[found][1] = surname_value

                iup.set(txt_name, :VALUE, nil)
                iup.set(txt_surname, :VALUE, nil)
            }
            [true,true]
        } with {
            await :Iup.Action | it.h==btn_delete {
                val id_val = iup.get.number(lst_names, :VALUE)
                val id = iup.get.id(lst_names, :ID, id_val)
                val name_value = iup.get.pointer(txt_name, :VALUE) -> to-string -> trim
                val surname_value = iup.get.pointer(txt_surname, :VALUE) -> to-string -> trim

                val found = loop i in {0 => db_names_count{ {
                    if (db_names_list[i][2] == id){
                        break(i)
                    }
                }

                set db_names_count = db_names_count - 1

                loop j in {found => db_names_count{ {
                    set db_names_list[j] = copy(db_names_list[j + 1])
                }

                println(db_names_list)
                println(db_names_count)

                iup.set(txt_name, :VALUE, nil)
                iup.set(txt_surname, :VALUE, nil)
            }
            [true,true]
        } with {
            await :Iup.Value | it.h==txt_filter {}
            [true,false]
        } with {
            await :Iup.Value | it.h==lst_names {
                val id_val = iup.get.number(lst_names, :VALUE)
                val id = iup.get.id(lst_names, :ID, id_val)

                val found = loop i in {0 => db_names_count{ {
                    if (db_names_list[i][2] == id){
                        break(i)
                    }
                }

                iup.set(txt_name, :VALUE, db_names_list[found][0])
                iup.set(txt_surname, :VALUE, db_names_list[found][1])
            }
            [false,true]
        }


        if loadupdate[0] == true {   
            val filter_text = iup.get.pointer(txt_filter,:VALUE) -> to-string -> trim
            var fil = true
            var j = 1
            var temp_surname
            var temp_name
            var temp_id
            ;;    var temp :FName;
            
            if (#filter_text == 0) {
                set fil = false
            }

            iup.set(lst_names, :1, nil)

            loop i in {0 => db_names_count{ {
                set temp_id = db_names_list[i][2]
                set temp_surname = db_names_list[i][1]
                set temp_name = db_names_list[i][0]
                if ((not fil) or (filterName(temp_surname, filter_text))) {
                    `IupSetStrfId($lst_names.Pointer, "", $j.Number, "%s, %s", $temp_surname.Dyn->Vector.buf, $temp_name.Dyn->Vector.buf);`
                    `IupSetIntId($lst_names.Pointer, "ID", $j.Number, $temp_id.Number);`
                    set j = j + 1
                }        
            }
            iup.set(lst_names, :VALUE, nil)
        }
        if loadupdate[1] == true {
            if iup.get.number(lst_names, :VALUE) == 0 {
                iup.set(btn_update, :ACTIVE, "NO")
                iup.set(btn_delete, :ACTIVE, "NO")
            } else {
                iup.set(btn_update, :ACTIVE, "YES")
                iup.set(btn_delete, :ACTIVE, "YES")
            }
        }
    }
}

iup.loop()

